/**
 * 慧燈2019PTWA軌道障礙賽基本程式
 * 使用4顆 Sharp IR GP2Y0A41SK0F 紅外線測距模組
 * 
 */

/**
   感應器區 sensor measurement
*/
#include "ptwa_nano_ultrasound_track.h"
#define RIGHTEYE A0
#define MIDLEFTEYE A1
#define MIDRIGHTEYE A4
#define LEFTEYE A3

const int RIGHT = 0, MID = 1, MIDLEFT = 1, MIDRIGHT = 2, LEFT = 3;
int sensorValue[4] = {0}; //0=right 1=mid 2= midRight 3=left

/**
   車子規格區 car measurement
*/
const int wd = 1070;//兩輪中心距離 in 1/100cm
const int carWidth = 1310; // in 1/100cm
const int carLen = 1180; // in 1/100cm


/**
   速度、距離、PID
*/


int tP = 120;
const int turn90MS = 280;//轉90度所需時間
const int safeDistance = 1000; //tp=時，看到障礙物到完全煞停的所需距離 用safeDistTest()測,1/100 cm
const int turnSD = safeDistance + 300; //障礙物前煞停+轉90度， 用turnSDTest()測,1/100 cm
const int carVec = 5000;//車子以 tP 前進1秒的距離，1/100cm per second
//PID
const int pivot = 1700;//前進時要走在離牆多遠的線上 單位:1/100 cm, mm*10 cos(30)=0.866 cos(60)=0.5
float kP = 0.4;//正比於err造成的輪速改變 => 影響誤差反應幅度
float kD = 0;
const int errMax = int(tP / kP / 1.15); //err上限，影響 tP 可變範圍，越大則 tP 可變範圍越大，則可做出越尖銳的轉彎
const int errDeadzone = 10; // 1/100 cm 反應死區




/*
int tP = 70;
const int turn90MS = 410;//轉90度所需時間
const int pivot = 1700;//前進時要走在離牆多遠的線上 單位:1/100 cm, mm*10 cos(30)=0.866 cos(60)=0.5
float kP = 0.2;//正比於err造成的轉彎幅度
float kD = 0;
const int errMax = int(tP / kP / 1.2); //越小則轉彎半徑越大
const int errDeadzone = 10; // 1/100 cm
const int safeDistance = 500; //tp=時，看到障礙物到完全煞停的所需距離 用safeDistTest()測,1/100 cm
const int turnSD = safeDistance + 300; //障礙物前煞停+轉90度， 用turnSDTest()測,1/100 cm
const int carVec = 3000;//車子以 tP 前進1秒的距離，1/100cm per second
*/


int err = 0;
int lastErr = 0;
int deltaErr = 0;

/**
   靠左/右牆
*/
//int FOLLOWWALL = LEFT;
int FOLLOWWALL = RIGHT;

void setup()
{
  Serial.begin(115200);
  pinMode(LEFTEYE, INPUT);
  pinMode(MIDLEFTEYE, INPUT);
  pinMode(RIGHTEYE, INPUT);
  pinMode(MIDRIGHTEYE, INPUT);
  pinMode(2, OUTPUT);
  pinMode(3, OUTPUT);
  pinMode(4, OUTPUT);
  pinMode(5, OUTPUT);

  (digitalRead(12) == 1) ? FOLLOWWALL = LEFT : FOLLOWWALL = RIGHT;


}

void loop()
{
  /*每次tP改變或是換車體時，請做以下測試，並將所得數值填入上方定義區
   * 測試OKed
   * turn90Test(280);
   * safeDistTest(1000);
   * turnSDTest(safeDistance + 300);
   * carVecTest();//=500
   * readSensors();//拿一個紙箱測試PID直到能沿邊繞
   * moveForward();//test PID
   */
  


  readSensors();
  if ( isFrontWalled() && isRightWalled() ) {
    //shiftLaneTo(LEFT);
    //shiftLaneTo(RIGHT);
    pingandLeftTurn90();
    //while (true) brake();
  }
  else {
    moveForward();
  }






  /**
    TODO 尚未完成
  */
  /*
    //靠牆走
    //如果遇到障礙物
    //切換到另一邊牆
    if (isPathForward()) {
    moveForward();
    }
    else {
    if (FOLLOWWALL == RIGHT) {
    brake();
    //turnLeft();
    shiftLaneTo(LEFT);
    FOLLOWWALL = LEFT;
    }
    else {
    brake();
    //turnRight();
    shiftLaneTo(RIGHT);
    FOLLOWWALL = RIGHT;
    }
    }
  */
}

/**
 * 車子檢測用函數區
 */
void pwmTest() {
  for (int i = 0; i <= 200; i += 10) {
    motorsMs(i, i, 1000);
    brake();
  }
}
void safeDistTest(int dd) { //測safeDistance
  readSensors();
  if (sensorValue[MID] <= dd ) while (true) brake();  //TODO 需實測
  else motors(tP, tP);
}
void turnSDTest(int ss) {
  readSensors();
  if (sensorValue[MID] <= ss) { //TODO 需實測
    motorsMs(-tP, tP, turn90MS); //左轉90
    while (true) brake();
  }
  else {
    motors(tP, tP);
  }
}
void turn90Test(int ms) {
  motorsMs(-tP, tP, ms);
  while (true) brake();
}
void carVecTest() {
  motorsMs(tP, tP, 1000);
  while (true) brake();
}

/**
  車子動作區 movement functions
*/
/**
   PD 跟隨左/右牆走
*/
void moveForward() {
  if (FOLLOWWALL == LEFT) {//左手法則
    err = pivot -  sensorValue[LEFT] ;
    err = crampErr(err);
    deltaErr = err -  lastErr;
    lastErr = err;
    motors(int(tP + err * kP + deltaErr * kD ), int(tP - err * kP - deltaErr * kD));
  }
  else if (FOLLOWWALL == RIGHT) { //右手法則
    err = pivot -  sensorValue[RIGHT];
    err = crampErr(err);
    deltaErr = err -  lastErr;
    lastErr = err;
    motors(int(tP - err * kP - deltaErr * kD), int(tP + err * kP + deltaErr * kD));
  }
}

int crampErr(int err) {//reference 龍華科技大學第五代迷宮電腦鼠論文
  if (err > errDeadzone) {
    err -= errDeadzone;
    err = min(errMax, err);
  }
  else if (err < -errDeadzone) {
    err += errDeadzone;
    err = max(-errMax, err);
  }
  else err = 0;
  return err;
}
void shiftLaneTo(int LA) {//TODO 尚未完成 換跑道
  int a; (LA == LEFT) ? a = 1 : a = -1;
  int v = tP;
  motorsMs(-v * a, v * a, turn90MS / 2); //轉45度 cos(45) = 0.525
  motorsMs(v, v, pm2TimeMS(150));//前進 in 1/100cm
  motorsMs(v * a, -v * a, turn90MS / 1.25); //抵消慣性轉45度
  //brake();
}

void turn90(int dir) {//盲轉90度
  int a;
  (dir == LEFT) ? a = 1 : a = -1;
  motorsMs(-a * tP, a * tP, turn90MS);
}
void pingandLeftTurn90() {//左轉90度，時間用盡 或 前面沒牆了 則停止
  int tt = turn90MS;
  brake();
  readSensors();
  motors(-tP, tP);
  while (tt > 0 && isFrontWalled()) {
    tt -= 1;
    delay(1);
  }
  //while(true) brake();
}

float pm2TimeMS(float s) {//s in 1/100 cm 輸入距離s轉為以 carVec 前進所需時間
  float error = 200.0;//實測誤差約少20mm
  return (s + error) / carVec * 1000;
}
/**
   馬達函數 motor functions
*/
/**
   motorsMs(左馬達出力, 右馬達出力, 持續毫秒數)
   函數執行完畢，馬達狀態會維持住，除非呼叫其他馬達函數。
*/
void motorsMs(int L, int R, int ms) {
  motors(L, R);
  delay(ms);
}

/**
   motors(左馬達出力, 右馬達出力)
   函數執行完畢，馬達狀態會維持住，除非呼叫其他馬達函數。
*/
void motors(int L, int R) {
  /*
    Serial.print(L);
    Serial.print(" --- ");
    Serial.println(R);
  */
  int bias = 0;
  digitalWrite(4, L >= 0 ? 0 : 1);
  analogWrite(5, L >= 0 ? min(L, 255) : min(255 + L, 255));
  digitalWrite(2, R >= 0 ? 0 : 1);
  analogWrite(3, R >= 0 ? min(R - bias, 255) : min(255 + R - bias, 255));
}

/**
   馬達不出力，滑行n毫秒
*/
void coast(int n) {
  digitalWrite(2, 0);
  analogWrite(3, 0);
  digitalWrite(4, 0);
  analogWrite(5, 0);
  delay(n);
}

/*
   馬達煞停，會有立即的停頓感
*/
void brake() {
  digitalWrite(2, 1);
  analogWrite(3, 255);
  digitalWrite(4, 1);
  analogWrite(5, 255);
}


/**
   感測器區sensor functions
*/
void readSensors() {
  sensorValue[RIGHT] = getDistanceCentCM((analogRead(RIGHTEYE) + analogRead(RIGHTEYE)) / 2);
  sensorValue[MIDRIGHT] = getDistanceCentCM((analogRead(MIDRIGHTEYE) + analogRead(MIDRIGHTEYE)) / 2);
  sensorValue[MIDLEFT] = getDistanceCentCM((analogRead(MIDLEFTEYE) + analogRead(MIDLEFTEYE)) / 2);
  sensorValue[LEFT] = getDistanceCentCM((analogRead(LEFTEYE) + analogRead(LEFTEYE)) / 2);
  Serial.print(sensorValue[LEFT]); Serial.print(",");
  Serial.print(sensorValue[MIDLEFT]); Serial.print(",");
  Serial.print(sensorValue[MIDRIGHT]); Serial.print(",");
  Serial.println(sensorValue[RIGHT]);

}
bool isRightWalled(){
  sensorValue[RIGHT] = getDistanceCentCM(analogRead(RIGHTEYE));
  if(sensorValue[RIGHT] < pivot * 1.5) return true;
  else return false;
}
bool isFrontWalled(){
  sensorValue[MIDLEFT] = getDistanceCentCM(analogRead(MIDLEFTEYE));
  if(sensorValue[MIDLEFT] <= turnSD) return true;
  else return false;
}
int getDistanceCentCM(int v) { //reference: https://github.com/jeroendoggen/Arduino-distance-sensor-library
  int adcValue = v;
  if (adcValue > 600)                             // lower boundary: 4 cm (3 cm means under the boundary)
  {
    return (3 * 100);
  }

  if (adcValue < 80 )                             //upper boundary: 36 cm (returning 37 means over the boundary)
  {
    return (37 * 100);
  }

  else
  {
    //return (1 / (0.000413153 * adcValue - 0.0055266887))*100;
    return (1 / (0.000413153 * adcValue - 0.0055266887)) * 100;
  }
}

