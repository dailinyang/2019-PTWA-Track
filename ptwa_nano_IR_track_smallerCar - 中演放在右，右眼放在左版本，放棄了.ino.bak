/**
   慧燈2019PTWA軌道障礙賽基本程式
   使用紅外線測距模組

*/

/*********************************************************************************
   感應器初始化區 sensor measurement
********************************************************************************/
#define RIGHTEYE A0
#define MIDLEFTEYE A1
#define MIDRIGHTEYE A4
#define LEFTEYE A3
const int RIGHT = 0, MID = 1, MIDLEFT = 1, MIDRIGHT = 2, LEFT = 3;
int sensorValue[4] = {0}; //RIGHT = 0, MID = 1, MIDLEFT = 1, MIDRIGHT = 2, LEFT = 3

/********************************************************************************
   速度、距離、PID 區
*******************************************************************************/

/*
//14500小電池測試區vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
bool DEBUG = false;
//tP速度下的車子參數 分區
int tP = 150;//基本速度
const int turn90MS = 90;//轉90度所需時間
const int safeDistance = 2100; //車速=tP時，看到障礙物到完全煞停的所需距離 用safeDistTest()測,1/100 cm
const int turnSD = safeDistance + 500; //障礙物前煞停+轉90度， 用turnSDTest()測,1/100 cm
const int carVec = 6900;//車子以 tP 前進1秒的距離，1/100cm per second
//PID
const int pivot = 1400;//前進時要走在離牆多遠的線上 單位:1/100 cm, mm*10 cos(30)=0.866 cos(60)=0.5
float kP = 0.2 ;//0.2;0.125;//正比於err造成的輪速改變 => 影響誤差反應幅度。通常是小數
float kD = 1.5;//0.1;0.15;//誤差的微分
const int errMax = int(tP / kP * 1.007);//1.1);//int(tP / kP * 1.0099); //err上限，影響 tP 可變範圍，越大則 tP 可變範圍越大，則可做出越尖銳的轉彎
const int errDeadzone = 10; // 1/100 cm, 反應死區
//^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
 */





//14500小電池測試區vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
bool DEBUG = false;
//tP速度下的車子參數 分區
int tP = 100;//基本速度
const int turn90MS = 170;//轉90度所需時間
const int safeDistance = 1200; //車速=tP時，看到障礙物到完全煞停的所需距離 用safeDistTest()測,1/100 cm
const int turnSD = 2500;//safeDistance + 500; //障礙物前煞停+轉90度， 用turnSDTest()測,1/100 cm
const int carVec = 6900;//車子以 tP 前進1秒的距離，1/100cm per second
//PID
const int pivot = 2000;//前進時要走在離牆多遠的線上 單位:1/100 cm, mm*10 cos(30)=0.866 cos(60)=0.5
float kP = 0.125 ;//0.2;0.125;//正比於err造成的輪速改變 => 影響誤差反應幅度。通常是小數
float kD = 1.0;//0.1;0.15;//誤差的微分

const int errMax = int(tP / kP * 1);//1.02);//1.01);//int(tP / kP * 1.0099); //err上限，影響 tP 可變範圍，越大則 tP 可變範圍越大，則可做出越尖銳的轉彎
const int errDeadzone = 10; // 1/100 cm, 反應死區
//^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


//PD初始化
int err = 0;//誤差
int lastErr = 0;//上迴圈的誤差
int deltaErr = 0;//本次與上次迴圈誤差的差值



/*********************************************************************************
  主程式從這裡開始會執行setup()一次，然後進loop()無限循環
 *********************************************************************************/

/**
   起始設定，讀第12隻腳決定開機後要沿哪一牆，HIGH=LEFT, LOW=RIGHT;
*/
void setup()
{
  Serial.begin(115200);
  pinMode(LEFTEYE, INPUT);
  pinMode(MIDLEFTEYE, INPUT);
  pinMode(RIGHTEYE, INPUT);
  pinMode(MIDRIGHTEYE, INPUT);
  pinMode(2, OUTPUT);
  pinMode(3, OUTPUT);
  pinMode(8, OUTPUT);
  pinMode(9, OUTPUT);
}

/**
   無限迴圈的主程式
*/
void loop()
{
  
  //motors(255, 255);//Leo on 20190729
  //
  /*每次tP改變或是換車體時，請做以下測試，並將所得數值填入上方 【tP速度下的車子參數 分區】
     測試OKed
    turn90Test(90);
    safeDistTest(1300);
    turnSDTest(safeDistance + 300);
     carVecTest();//690
     readSensors();//這兩行拿一個紙箱測試PID直到能沿邊繞
    moveForward();//這兩行拿一個紙箱測試PID直到能沿邊繞
  */




  readSensors();
  //if ( isFrontWalled() && isRightWalled() ) {
  if ( isFrontWalled()) {
    pingandLeftTurn90();
    stop();
  }
  /*
  //track不能這樣判斷，有可能有小右彎，試試加大右眼範圍?=>也不行，會誤判
  else if(!isRightWalled()){
      motorsMs(0,0,50);//右邊沒牆，等輪子走到轉角
      while(!isRightWalled()) moveForward();
    }
  */
  //else if(isFrontWalled()) FTurn90();
  //else if(( sensorValue[MIDLEFT] < sensorValue[RIGHT] )) LT();
  else moveForward();
}
 

//vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
//以下是基本功能區，包含有 1.車子基本動作 2.馬達函數區 3.感測器區 4.車子檢測用函數區
//                       5.PID用函數區

/********************************************************************************
  1.車子基本動作區 movement functions
*******************************************************************************/

/**
   扶著牆往前走，使用前需呼叫 readSensors()
*/
void moveForward() //
{
  //右手法則
  err = pivot -  sensorValue[RIGHT];
  err = crampErr(err);
  deltaErr = err -  lastErr;
  lastErr = err;
  motors(int(tP - err * kP - deltaErr * kD), int(tP + err * kP + deltaErr * kD));
}

/**
   左轉90度，時間用盡 或 前面沒牆了 則停止
*/
void pingandLeftTurn90()
{
  int tt = turn90MS;
  ABSStop(10);
  readSensors();
  motors(-tP, tP);
  //左轉直到前沒牆
  //while (tt > 0 && isFrontWalled() && isRightWalled() ) {
  while (tt > 0 && isFrontWalled() ) {
    tt -= 1;
    delay(1);
  }
  //右眼走第1面障礙物牆
  while(isRightWalled()){
    moveForward();
  }
  //等輪子到轉彎邊上
  motorsMs(0,0,50);
  //右轉(不用做了，交給PID)?
  
}

/**
   左轉90度，時間用盡 或 前面沒牆了 則停止
*/
void FTurn90()
{
  int tt = turn90MS;
  //brake();
  //ABSStop(10);
  readSensors();
  motors(-tP, tP);
  while (tt > 0 && isFrontWalled() ) {
    tt -= 1;
    delay(1);
  }
}


/*
pingandLeftTurn90 version 2
 */
void LT(){
  int tt = turn90MS;
  ABSStop(10);
  readSensors();
  motors(-tP, tP);
  while (tt > 0 && (sensorValue[MIDLEFT] < sensorValue[RIGHT]) ) {
    tt -= 1;
    readSensors();
    delay(1);
  }
}

/********************************************************************************
   2.馬達函數區 motor functions
*******************************************************************************/

/**
   motorsMs(左馬達出力, 右馬達出力, 持續毫秒數)
   函數執行完畢，馬達狀態會鎖住，除非呼叫其他馬達函數。
*/
void motorsMs(int L, int R, int ms) //驅動馬達(左馬達出力, 右馬達出力,持續ms)
{
  motors(L, R);
  delay(ms);
}

/**
   motors(左馬達出力, 右馬達出力)
   函數執行完畢，馬達狀態會鎖住，除非呼叫其他馬達函數。
*/
void motors(int L, int R) //驅動馬達(左馬達出力, 右馬達出力)
{
  int bias = 0;
  digitalWrite(8, L >= 0 ? 0 : 1);
  analogWrite(9, L >= 0 ? min(L, 255) : min(255 + L, 255));
  digitalWrite(2, R >= 0 ? 0 : 1);
  analogWrite(3, R >= 0 ? min(R - bias, 255) : min(255 + R - bias, 255));
}

/**
   馬達不出力，滑行n毫秒
*/
void coast(int n)//馬達滑行
{
  digitalWrite(2, 0);
  analogWrite(3, 0);
  digitalWrite(8, 0);
  analogWrite(9, 0);
  delay(n);
}

/*
   馬達煞車，會有立即的停頓感
*/
void brake() //馬達煞停
{
  digitalWrite(2, 1);
  analogWrite(3, 255);
  digitalWrite(8, 1);
  analogWrite(9, 255);
}

/*
  ABSStop(煞車持續時間)，輸入10的倍數
  持續點踩煞車一段時間，執行完不一定車子停止。
*/
void ABSStop(unsigned int ms) {
  int tt = 0;
  while (tt < ms) {
    brake();
    delayMicroseconds(9000);
    motors(0, 0);
    delayMicroseconds(1000);
    tt += 10;
  }
}

/*
  煞車後維持停止
*/
void stop()
{
  brake(); while (true);
}

/*******************************************************************************
   3.感測器區 sensor functions
********************************************************************************/

/**
   讀所有感應器，讀完會儲存在sensorValue[RIGHT],sensorValue[MIDRIGHT],
   sensorValue[MIDLEFT],sensorValue[LEFT]四個格子中
*/
void readSensors()
{
  sensorValue[RIGHT] = getDistanceCentCM((analogRead(RIGHTEYE) + analogRead(RIGHTEYE)) / 2);
  sensorValue[MIDRIGHT] = getDistanceCentCM((analogRead(MIDRIGHTEYE) + analogRead(MIDRIGHTEYE)) / 2);
  sensorValue[MIDLEFT] = getDistanceCentCM((analogRead(MIDLEFTEYE) + analogRead(MIDLEFTEYE)) / 2);
  sensorValue[LEFT] = getDistanceCentCM((analogRead(LEFTEYE) + analogRead(LEFTEYE)) / 2);
  if (DEBUG) {
    //Serial.print(sensorValue[LEFT]); Serial.print(",");
    Serial.print(sensorValue[MIDLEFT]); Serial.print(",");
    //Serial.print(sensorValue[MIDRIGHT]); Serial.print(",");
    Serial.println(sensorValue[RIGHT]);
  }

}

/**
   讀一次右眼，判斷右邊有牆嗎?判斷條件:sensorValue[RIGHT] < pivot * 常數(可修改)
   輸出: 有牆=true, 沒牆=false
*/
bool isRightWalled()  //
{
  sensorValue[RIGHT] = getDistanceCentCM(analogRead(RIGHTEYE));
  if (sensorValue[RIGHT] < pivot * 1.5) return true;
  else return false;
}

/**
   讀一次前眼，判斷前面有牆嗎?判斷條件:sensorValue[MIDLEFT] <= turnSD
   輸出: 有牆=true, 沒牆=false
*/
bool isFrontWalled()  //
{
  sensorValue[MIDLEFT] = getDistanceCentCM(analogRead(MIDLEFTEYE));
  if (sensorValue[MIDLEFT] <= turnSD) return true;
  else return false;
}

/**
   紅外線伏特換算成距離，單位為1/100公分
   reference: https://github.com/jeroendoggen/Arduino-distance-sensor-library
*/
int getDistanceCentCM(int v)
{
  int adcValue = v;
  if (adcValue > 600)                             // lower boundary: 4 cm (3 cm means under the boundary)
  {
    return (3 * 100);
  }

  if (adcValue < 80 )                             //upper boundary: 36 cm (returning 37 means over the boundary)
  {
    return (37 * 100);
  }

  else
  {
    //return (1 / (0.000413153 * adcValue - 0.0055266887))*100;
    return (1 / (0.000413153 * adcValue - 0.0055266887)) * 100;
  }
}

/*****************************************************************
   4.車子檢測用函數區
 *****************************************************************/

/**
   測試PWM功能是否正常
*/
void pwmTest()
{
  for (int i = 0; i <= 200; i += 10) {
    motorsMs(i, i, 1000);
    brake();
  }
}

/**
   車子在dd距離時開始煞車，用來取得safeDistance
*/
void safeDistTest(int dd)
{
  motors(tP, tP);
  while (true) {
    readSensors();
    if (sensorValue[MID] <= dd ) stop();
    else break;
  }

}

/*
  車子在ss距離時開始煞停+轉90度，用來取得turnSD
*/
void turnSDTest(int ss)
{
  readSensors();
  if (sensorValue[MID] <= ss)
  {
    motorsMs(-tP, tP, turn90MS); //左轉90
    stop();
  }
  else {
    motors(tP, tP);
  }
}

/*
  車子持續迴轉 ms 後停止，用以取得迴轉90度時間
*/
void turn90Test(int ms)
{
  motorsMs(-tP, tP, ms);
  stop();
}

/*
  測車子每秒能走多遠
*/
void carVecTest()
{
  motorsMs(tP, tP, 1000);
  stop();
}


/*****************************************************************
   5.PID用函數區
 *****************************************************************/
int crampErr(int err) //限制err的上下限   reference 龍華科技大學第五代迷宮電腦鼠論文
{
  if (err > errDeadzone) {
    err -= errDeadzone;
    err = min(errMax, err);
  }
  else if (err < -errDeadzone) {
    err += errDeadzone;
    err = max(-errMax, err);
  }
  else err = 0;
  return err;
}
